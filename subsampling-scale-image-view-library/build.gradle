apply plugin: 'com.android.library'

group = 'com.davemorrissey.labs'
archivesBaseName = 'subsampling-scale-image-view'
version = '3.10.0'

android {
    compileSdkVersion 28

    defaultConfig {
        consumerProguardFiles 'proguard-rules.txt'
        minSdkVersion 14
        targetSdkVersion 28
    }
}

configurations {
    javadocs
}

dependencies {
    javadocs 'androidx.annotation:annotation:1.0.0-beta01'
    javadocs 'androidx.exifinterface:exifinterface:1.0.0-beta01'
    implementation 'androidx.annotation:annotation:1.0.0-beta01'
    implementation 'androidx.exifinterface:exifinterface:1.0.0-beta01'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    destinationDir = rootProject.file("docs/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    classpath += configurations.javadocs
    options.noTimestamp = true
    options.links = ['https://developer.android.com/reference/']
}
